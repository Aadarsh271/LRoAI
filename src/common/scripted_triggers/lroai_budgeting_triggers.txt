lroai_in_default = {
    scaled_debt >= 0.975
    lroai_country_budget_surplus < 0
}

lroai_budget_health_is_equal_or_higher = {
    switch = {
        trigger = $level$
        -3 = {
            lroai_weeks_of_reserves < lroai_weeks_of_reserves_for_health_0
            OR = {
                lroai_country_budget_surplus_percent >= lroai_country_budget_surplus_for_health_minus_3
                scaled_debt < lroai_scaled_debt_for_health_minus_3
            }
        }
        -2 = {
            lroai_weeks_of_reserves < lroai_weeks_of_reserves_for_health_0
            OR = {
                lroai_country_budget_surplus_percent >= lroai_country_budget_surplus_for_health_minus_2
                scaled_debt < lroai_scaled_debt_for_health_minus_2
            }
        }
        -1 = {
            lroai_weeks_of_reserves < lroai_weeks_of_reserves_for_health_0
            OR = {
                lroai_country_budget_surplus_percent >= lroai_country_budget_surplus_for_health_minus_1
                scaled_debt < lroai_scaled_debt_for_health_minus_1
            }
        }
        0 = {
            OR = {
                lroai_country_budget_surplus_percent >= lroai_country_budget_surplus_for_health_0
                lroai_weeks_of_reserves >= lroai_weeks_of_reserves_for_health_0
            }
        }
        1 = {
            OR = {
                lroai_country_budget_surplus_percent >= lroai_country_budget_surplus_for_health_1
                lroai_weeks_of_reserves >= lroai_weeks_of_reserves_for_health_1
            }
        }
        2 = {
            OR = {
                lroai_country_budget_surplus_percent >= lroai_country_budget_surplus_for_health_2
                lroai_weeks_of_reserves >= lroai_weeks_of_reserves_for_health_2
            }
        }
        3 = {
            OR = {
                lroai_country_budget_surplus_percent >= lroai_country_budget_surplus_for_health_3
                lroai_weeks_of_reserves >= lroai_weeks_of_reserves_for_health_3
            }
        }
        fallback = {
            always = no
        }
    }
}

lroai_is_using_military_forces = {
    OR = {
        is_at_war = yes
        is_diplomatic_play_committed_participant = yes
        any_scope_general = {
            is_mobilized = yes
        }
        any_scope_admiral = {
            is_mobilized = yes
        }
    }
}

lroai_are_military_expenses_higher_than_usual = {
    OR = {
        has_variable = lroai_military_expenses_are_higher_than_usual
        lroai_is_using_military_forces = yes
    }
}

lroai_tax_level_equal_or_lower = {
    trigger_if = {
        limit = {
            $value$ = 5
        }
        OR = {
            tax_level = very_low
            tax_level = low
            tax_level = medium
            tax_level = high
            tax_level = very_high
        }
    }
    trigger_else_if = {
        limit = {
            $value$ = 4
        }
        OR = {
            tax_level = very_low
            tax_level = low
            tax_level = medium
            tax_level = high
        }
    }
    trigger_else_if = {
        limit = {
            $value$ = 3
        }
        OR = {
            tax_level = very_low
            tax_level = low
            tax_level = medium
        }
    }
    trigger_else_if = {
        limit = {
            $value$ = 2
        }
        OR = {
            tax_level = very_low
            tax_level = low
        }
    }
    trigger_else_if = {
        limit = {
            $value$ = 1
        }
        tax_level = very_low
    }
    trigger_else = {
        always = no
    }
}

lroai_tax_level_equal_or_higher = {
    trigger_if = {
        limit = {
            $value$ = 5
        }
        tax_level = very_high
    }
    trigger_else_if = {
        limit = {
            $value$ = 4
        }
        OR = {
            tax_level = high
            tax_level = very_high
        }
    }
    trigger_else_if = {
        limit = {
            $value$ = 3
        }
        OR = {
            tax_level = medium
            tax_level = high
            tax_level = very_high
        }
    }
    trigger_else_if = {
        limit = {
            $value$ = 2
        }
        OR = {
            tax_level = low
            tax_level = medium
            tax_level = high
            tax_level = very_high
        }
    }
    trigger_else_if = {
        limit = {
            $value$ = 1
        }
        OR = {
            tax_level = very_low
            tax_level = low
            tax_level = medium
            tax_level = high
            tax_level = very_high
        }
    }
    trigger_else = {
        always = no
    }
}


#
# tag           : tag_lroai_need_additional_ports_critical
# Called by     : lroai_allocate_building_port lroai_evaluate_building_port
#
lroai_need_additional_ports_critical = {
    lroai_total_convoy_produced_value < lroai_total_convoy_available_value_ceiling_critical
}


#
# tag           : tag_lroai_need_additional_ports_high
# Called by     : lroai_allocate_building_port lroai_evaluate_building_port
#
lroai_need_additional_ports_high = {
    lroai_total_convoy_produced_value < lroai_total_convoy_available_value_ceiling_high
}

#
# tag           : tag_lroai_need_additional_ports_medium
# Called by     : lroai_allocate_building_port lroai_evaluate_building_port
#
lroai_need_additional_ports_medium = {
    lroai_total_convoy_produced_value < lroai_total_convoy_available_value_ceiling_medium
}

#
# tag           : tag_lroai_need_additional_ports_low
# Called by     : lroai_allocate_building_port lroai_evaluate_building_port
#
lroai_need_additional_ports_low = {
    lroai_total_convoy_produced_value < lroai_total_convoy_available_value_ceiling_low
}